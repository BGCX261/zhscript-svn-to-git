#summary 查找播放 flash 10.3 在本地缓冲的flv（仅适ff）

= 查找播放 flash 10.3 在本地缓冲的flv（仅适ff） =
{{{
#!/usr/lib/zhscript/gtkmmsh lib2/window1.glade --。
隧道标题、火狐正下。
隧道窗口宽高、500、400。
隧道窗口居中。

加载lib/defi。
加载lib/gjk4。
加载lib/gjke4。
加载lib/stdc。
加载lib/redireconsoi4。
加载lib/regexpr4。
加载lib/doscmd4。
加载lib/file4。
加载lib2/iconview.zs。

隧道可见、iconarea1、1。
赋予【主】图标夹以隧道图标夹、iconview1、代码为下代码
	图标夹获取‘图标夹’、下代码
		隧道内容、text、‘参数2’↙↙‘参数1’。
	上代码。
	如果存在参数3并且‘参数3’等于活性那么
		隧道点击、play。
上代码。
隧道代码、下代码
	如果‘肇事者’等于提示那么
		隧道内容、text、需要‘参数0’。
上代码。

隧道可见、exec2、1。
隧道提示文字、exec、“gnome-open *”。
隧道侦听、点击、exec、下代码
	别名程序以参数1。
	如果不‘程序’那么隧道内容、text、‘参数0’无设置，退出。
	图标夹获取‘图标夹’、下代码
		赋予程序以调用‘正则替’、‘程序’、(\*)、'‘参数1’'。
		隧道内容、text、‘程序’。
		执行‘程序’&。
	上代码。
上代码。

隧道提示文字、exec2、所有路径。
隧道侦听、点击、exec2、下代码
	隧道内容、text、。
	图标夹遍历‘图标夹’、下代码
		隧道插入、text、“ ‘参数1’”。
	上代码。
上代码。

隧道可见、play、_media、1。
隧道侦听、点击、play、下代码
	图标夹获取‘图标夹’、下代码
		赋予程序以“mplayer '‘参数1’'”。
		隧道内容、text、‘程序’。
		执行‘程序’&。
	上代码。
上代码。

隧道可见、save、_file、1。
隧道提示文字、save、复制到当前目录，重命名。
隧道侦听、点击、save、下代码
	图标夹获取‘图标夹’、下代码
		别名文件以参数1。
		赋予目标以调用‘唯一文件名’、.、先调用‘得文件主名’、‘文件’了先调用‘得扩展名’、‘文件’了。
		赋予命令以“cp '‘文件’' '‘目标’'”。
		隧道内容、text、‘命令’。
		执行‘命令’。
	上代码。
上代码。

隧道可见、refresh、_view、1。
隧道侦听、点击、refresh、下代码
	赋予1以1。
	解释‘重定向输出三’、下代码
		别名符以参数1。
		如果‘符’等于字符10那么
			赋予【上】1以算术‘1’+1。
		否则先
			赋予【上】pid之‘1’以先如果存在pid之‘1’那么‘pid之‘1’’了‘符’。
		了。
	上代码、、“pgrep plugin-co”。
	赋予pid数目以算术‘1’-1。
	赋予pid数目2以1。
	
	赋予1以。
	迭代数组下代码
		别名pid以参数1。
		赋予目录以/proc/‘pid’/fd。
		显示‘目录’↙。
		解释‘重定向输出三’、下代码
			别名符以参数1。
			如果‘符’等于字符10那么
				赋予【上】【上】1以‘1’→‘目录’。
			赋予【上】【上】1以‘1’‘符’。
		上代码、、“ls -l ‘目录’”。
	上代码、pid。

	赋予行数目以0。
	迭代正则‘1’、“ ([0-9]+) -> ”([^→]+)→([^↙]+)↙、下代码
		赋予标识以FlashXX。
		如果调用‘头匹配方法’、‘参数2’、/tmp/‘标识’那么
			增至数组【上】行、2、先调用‘正则格’、‘参数2’、(‘标识’[^“ ”]+)了、‘参数3’/‘参数1’。
	上代码。

	隧道图标夹为清空、‘图标夹’。
	迭代数组下代码
		隧道图标夹为增加、	‘参数2’、‘参数1’、、、‘图标夹’。
	上代码、行。
上代码。
隧道点击、refresh。

}}}