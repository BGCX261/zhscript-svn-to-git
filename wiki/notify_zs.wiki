#summary 代码：类似 notify-send 用于弹出通知框

= 类似 notify-send 用于弹出通知框 =
不具备鼠标穿透，但可以指定位置、宽高、图片宽高、不透明度

允许多张图片，点击图片将直接关闭通知框，此时的返回码为图片的序号
{{{
#!/usr/lib/zhscript/gtkmmsh lib2/window1.glade --。
赋予内容以。
赋予w以500。
赋予h以120。
赋予y以50。
赋予x以sw-w-100。
赋予t以10s。
赋予o以0.85。
赋予iw以64。
赋予ih以64。

加载lib/defi。
加载lib/gjk4。
加载lib/clpars4。
调用‘命令行加回调’、
	、文字……、1、【代码】“赋予内容【上】以‘内容’‘参数1’↙。”、
	-t|--expire-time、‘t’、1、下代码
		赋予t【上】以‘参数1’。
	上代码、
	-i|--icon、图片、1、下代码
		别名图以参数1。
		显示‘图’↙。
		加载lib/doscmd4。
		如果调用‘exist’、‘图’否则再来。
		隧道位置、hpaned1、算术‘iw’+52。
		隧道可见、iconarea1、1。
		如果不存在图标夹那么
			赋予图标夹【主】以隧道图标夹为iconview1、图片缓存为隧道图片缓存、代码为下代码
				隧道返回码为‘参数1’。
				隧道终止。
			上代码。
		隧道读图片限制、‘iw’、‘ih’。
		隧道图标夹、增加、算术i=i+1、、‘图’、、‘图标夹’。
	上代码、
	-w|-h|-x|-y|-o|-iw|-ih、‘w’,‘h’,‘x’,‘y’,‘o’,‘iw’,‘ih’、1、下代码
		赋予名以调用‘子串方法’、‘参数0’、1。
		别名值以参数1。
		显示‘名’→‘值’↙。
		赋予‘名’【上】以‘值’。
	上代码、
	-?|--help、、0、下代码
		隧道提示、先调用‘命令行帮助’了、‘参数0【主】’。
		隧道终止。
		结束。
	上代码。
调用‘命令行解析’、‘参数栈’。
加载lib/gjke4。
迭代解释下代码
	别名名以参数1。
	赋予‘名’【上】以调用‘串替换方法’、‘‘名’’、sw、隧道屏幕宽。
	赋予‘名’【上】以调用‘串替换方法’、‘‘名’’、sh、隧道屏幕高。
	赋予‘名’【上】以调用‘串替换方法’、‘‘名’’、w、‘w’。
	赋予‘名’【上】以调用‘串替换方法’、‘‘名’’、h、‘h’。
上代码、x、y。

隧道可见、toolbar1、statusbar、0。
隧道窗口宽高、‘w’、‘h’。
隧道无边框。
隧道不透明度、‘o’。
隧道置顶。
隧道跳过任务栏。
隧道移动、算术‘x’、算术‘y’。
隧道文本框、不可编辑、text。
隧道内容、text、‘内容’。
隧道标题、‘内容’。
隧道一次定时器、、‘t’、【代码】“隧道终止”。

隧道定时器、总在可见工作区、100、下代码
	加载lib/wnck4。
	如果不存在屏那么先
		赋予屏【主】以调用‘屏得’。
		显示屏‘屏’↙。
		如果不‘屏’那么删除屏【主】，退出。
		调用‘屏强更’、‘屏’。
	了。
	赋予窗以调用‘窗得’、隧道xid。
	显示窗‘窗’↙。
	如果不‘窗’那么退出。
	调用‘‘参数0’’、‘窗’。
	隧道定时器、‘参数0’、移除。
上代码。

}}}