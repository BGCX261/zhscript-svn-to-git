#summary 代码：用最新文件更新目录

= 用最新文件更新目录的脚本 =
{{{
#!/usr/bin/zhscript --。
赋予默认以
  /usr/lib/zhscript。
赋予等信息以赋予比较时间以1。
赋予删掉无以赋予只显尺寸异常以0。

加载lib/defi。
加载lib/gjk4。

加载lib/clpars4。
调用‘命令行加回调’、
	-tm、“不比较时间”、0、【代码】“赋予比较时间【上】以0。”、
	-e、“不显示等信息”、0、【代码】“赋予等信息【上】以0。”、
	-d、“删掉在源里无的文件”、0、【代码】“赋予删掉无【上】以1。”、
	-bs、“只显尺寸异常”、0、【代码】“赋予只显尺寸异常【上】以1。”、
	、、1、下代码
		如果不存在zs那么赋予zs【上】以‘参数1’，再来。
		如果不存在.那么赋予.【上】以‘参数1’，再来。
	上代码、
	-h | --help、、0、下代码
		显示‘参数0【主】’下原样 [源] [目标] [选项]

源	默认是上原样‘默认’下原样，可用-代替
目标	默认是.
选项：
上原样调用‘命令行帮助’。
		结束。
	上代码。
调用‘命令行解析’、‘参数栈’。

如果不存在zs或者‘zs’等于-那么赋予zs以‘默认’。
如果调用‘尾匹配方法’、‘zs’、/否则赋予zs以‘zs’/。
显示#‘zs’→。
加载lib/doscmd4。
如果调用‘existdir’、‘zs’否则显示不存在↙，结束。
显示↙。

加载lib/file4。
如果不存在.那么赋予.以.。
如果调用‘尾匹配方法’、‘.’、/否则赋予.以‘.’/。
显示#‘.’→。
如果调用‘existdir’、‘.’否则显示不存在↙，结束。
如果‘.’等于./那么先
	加载lib/stdc。
	赋予.以先调用‘得当前目录函数’了/。
	显示‘.’→。
了。
显示↙。

加载lib/regexpr4。
赋予名以调用‘正则格’、‘.’、([^/]+)/$。
显示#‘名’。
循环先
	如果调用‘exist’、‘.’‘名’那么跳出。
	赋予名以调用‘正则格’、‘名’、(.+)[0-9]+$。
	显示→‘名’。
	如果调用‘exist’、‘.’‘名’那么跳出。
	赋予名以ui。
	显示→‘名’。
	如果调用‘exist’、‘.’‘名’那么跳出。
	赋予名以install。
	显示→‘名’。
	如果调用‘exist’、‘.’‘名’那么跳出。
	删除名。
	跳出。
了。
显示↙。

定义得时以下代码
	赋予时以调用‘fileinfo’、‘参数1’、%tm。
	如果不‘时’那么退出。
	调用‘串格给时间方法’、%Y%m%d.%H%M%S、‘时’。
上代码。
定义得尺以下代码
	调用‘fileinfo’、‘参数1’、%s。
上代码。
定义得权以下代码
	加载lib/redireconsoi4。
	调用‘子串方法’、先调用‘重定向输出’、“md5sum -b '‘参数1’'”了、0、32。
上代码。
遍历目录下代码
	赋予源以‘zs’如果存在名并且‘参数1’等于‘名’那么gtkmmsh否则‘参数1’。
	赋予标以‘.’‘参数1’。
	如果调用‘exist’、‘源’否则先
		如果‘删掉无’那么显示“rm '‘标’'”↙。
		再来。
	了。
	如果‘比较时间’那么先
		赋予源时以得时‘源’。如果不‘源时’那么显示#‘源’不“得时”↙，再来。
		赋予标时以得时‘标’。	如果不‘标时’那么显示#‘标’不“得时”↙，再来。
		如果‘源时’等于‘标时’那么先
			如果‘等信息’那么
				显示#‘源时’→‘参数1’↙。
			再来。
		了。
		如果‘源时’小于‘标时’那么显示#‘源时’:‘标时’→‘参数1’↙，再来。
	了。
	
	赋予源权以得权‘源’。
	赋予标权以得权‘标’。
	如果‘源权’等于‘标权’那么先
		如果‘等信息’那么
			显示#‘源权’→‘参数1’↙。
		再来。
	了。
	
	赋予源尺以得尺‘源’。
	赋予标尺以得尺‘标’。
	赋予源时以得时‘源’。
	赋予标时以得时‘标’。
	如果‘只显尺寸异常’那么先
		如果‘标尺’大于算术‘源尺’*2那么
			显示#“‘源时’ ‘源尺’”→‘源’↙
				#“‘标时’ ‘标尺’”→‘标’↙。
		再来。
	了。
	
	显示#“‘源权’ ‘源时’ ‘源尺’”↙
		#“‘标权’ ‘标时’ ‘标尺’”↙。
	显示“cp -p '‘源’' '‘标’'”↙。
上代码、‘.’、、s。

}}}