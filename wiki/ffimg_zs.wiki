#summary 代码：取出火狐缓冲里的所有图片

= 取出火狐缓冲里的所有图片 =

{{{
#!/usr/bin/zhscript --。
加载lib/defi。
定义“显示一”以“显示#‘参数1’↙。”。
定义“执行一”以“显示‘参数1’↙。”。
赋予命名以数。
赋予命令头以“ln -sf”。

加载lib/clpars4。
加载lib/gjk4。
调用‘命令行加回调’、
	-x、“直接执行方式”、0、下代码
		定义“执行一【主】”以“执行显示‘参数1’↙。”。
	上代码、
	-nm2、“以原名命名，默认是“‘命名’””、0、下代码
		赋予命名【主】以原。
	上代码、
	-cp、“复制文件，默认是“‘命令头’””、0、下代码
		赋予命令头【主】以cp。
	上代码、
	、指定火狐账户、1、下代码
		如果调用‘头匹配方法’、‘参数1’、-那么
			调用‘命令行解析’、--help。
		赋予账户【主】以‘参数1’。
	上代码、
	--help、、0、下代码
		显示‘参数0【主】’↙调用‘命令行帮助’。
		结束。
	上代码。
调用‘命令行解析’、‘参数栈’。

如果不存在账户那么
	赋予账户以default。
加载lib/redireconsoi4。
赋予目录以调用‘重定向输出’、“echo ~/.mozilla/firefox/”*.‘账户’/Cache/。
加载lib/regexpr4。
迭代正则‘目录’、([^↙]+)、下代码
	赋予目录【主】以‘参数1’。
	显示一‘目录’。
上代码。
如果调用‘串位置方法’、‘目录’、*大于0那么先
	显示账户‘账户’不存在↙。
	结束。
了。

加载lib/file4。
赋予联系单位以/tmp/先调用‘得文件主名’、‘参数0’了_zs。
执行一“mkdir -p ‘联系单位’;rm ‘联系单位’/”*。
（加载lib/filetype。）
定义文件处理以下代码
	别名子路径以参数1。
	赋予路径以‘目录’/‘子路径’。
	（赋予类型以文件类型‘路径’。）
	赋予类型以调用‘重定向输出’、“file '‘路径’'”。
	赋予类型以调用‘子串方法’、‘类型’、2、-1、‘路径’。
	如果调用‘串位置方法’、‘类型’、“ image data”大于0
	或者调用‘串位置方法’、‘类型’、“ image,”大于0
	那么先
		显示一‘类型’。
		赋予命令以“‘命令头’ '‘路径’' ‘联系单位’/”先
			当‘命名’先
				等于数那么算术i=i+1，跳出。
				等于原那么调用‘串替换方法’、‘子路径’、/、-，跳出。
			了。
		了。
		执行一‘命令’。
		退出。
	了。
	显示一‘子路径’→‘类型’。
上代码。

加载lib/doscmd4。
遍历目录下代码
	文件处理‘参数1’。
上代码、‘目录’、、s。

}}}