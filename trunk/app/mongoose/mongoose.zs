#!bin/gtkmmsh lib2/window1.glade ---。
隧道窗口宽高、600、400。隧道窗口居中。隧道标题为mongoose。
隧道可见、exec、_1、0。

加载preload.zs。
定义<<<<【主】【锁】以下代码
	隧道插入、text、‘参数1’↙、尾。
上代码。
定义命令【主】【锁】以下代码
	如果存在参数2那么
		执行‘参数1’。
	否则先
		显示<pre>。
		调用‘重定向输出/行’、下代码
			显示‘参数1’↙。
		上代码、下代码
			显示“<span style="color:white;background-color:red;">‘参数’</span>”↙。
		上代码、‘参数1’。
	了。
上代码。
定义“管理员命令”【主】【锁】以下代码
	命令“echo "‘sudo密码’"|sudo -S PATH=$PATH ‘参数1’&”。
上代码。
定义加密【主】【锁】以下代码
	调用‘urlencode’、‘参数1’、‘<<<<’。
上代码。
定义解密【主】【锁】以下代码
	调用‘urldecode’、‘参数1’、‘<<<<’。
上代码。

加载setting.zs。
隧道图标、‘应用目录’mongoose.png。
隧道最小化到托盘。

赋予root【主】【锁】以‘应用目录’root。
<<<<根→‘root’。
如果不存在上传目录那么先
	赋予上传目录【主】以‘root’/upload/。
	执行“mkdir -p ‘上传目录’”。
	<<<<上传目录→‘上传目录’。
了。

隧道代码、下代码
	如果‘肇事者’等于窗口那么先
		当‘参数0’先
			等于创建那么先
				加载lib/mongoose4。
				调用‘设置上传目录’、‘上传目录’。
				退出。
			了。
			等于销毁那么先
				加载lib/mongoose4-。
				退出。
			了。
		了。
	了。
上代码。

隧道可见、play、stop、_media、1。
隧道提示文字、play、启动。
隧道敏感、stop、0。

隧道侦听、点击、play、下代码
	<<<<“‘参数1’（‘参数0’）”。
	如果调用‘启动【mongoose4】’、‘端口号’、
	document_root	为‘root’、
	access_log_file	为/tmp/mongoose.log那么先
		隧道敏感、play、0。
		隧道敏感、stop、1。
	了否则先
		<<<<失败。
		隧道提示、“‘参数1’（‘参数0’）失败”。
	了。
上代码。

隧道侦听、点击、stop、下代码
	<<<<“‘参数1’（‘参数0’）”。
	隧道敏感、play、1。
	隧道敏感、stop、0。
	调用‘停止【mongoose4】’。
上代码。
